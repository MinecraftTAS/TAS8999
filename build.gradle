// configure plugins
plugins {
	// java application plugin
    id 'application'
	// shadow plugin
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// configure repositories
repositories {
	// add maven central
	mavenCentral()
}

// configure dependencies
dependencies {
	// jda discord api
	implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-beta.12'
	// logger
	implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

	// lombok
	compileOnly 'org.projectlombok:lombok:1.18.28'
	annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

// configure jar
base {
	// change compiled filename
	archivesName = "TAS8999-v2.0.0-SNAPSHOT"
}

// set application main class
application {
	mainClass = 'com.minecrafttas.tas8999.TAS8999'
}

// compile for java 20
java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_20
}

// upload tas8999 task [login via key]
tasks.register('upload', Exec) {
	workingDir "${buildDir}/libs"
	commandLine "cmd.exe /k echo put ${base.archivesName.get()}-all.jar | sftp -oPort=13524 pancake@mgnet.work".split(" ")
}

// copy tas8999 task
tasks.register('copyTAS8999', Exec) {
	workingDir "${buildDir}/libs"
	commandLine "ssh -p 13524 pancake@mgnet.work echo ${pancake_password} | sudo -S -u root cp /home/pancake/${base.archivesName.get()}-all.jar /home/tas/tas8999/tas8999.jar".split(" ")
}

// restart tas8999 task
tasks.register('restartTAS8999', Exec) {
	workingDir "${buildDir}/libs"
	commandLine "ssh -p 13524 pancake@mgnet.work echo ${pancake_password} | sudo -S -u root systemctl -M tas@ --user restart tas8999".split(" ")
}

upload.dependsOn = ['shadowJar']
upload.finalizedBy(copyTAS8999)
copyTAS8999.finalizedBy(restartTAS8999)
upload.group = 'tas8999'